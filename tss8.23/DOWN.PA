///	SYSTEM-DOWN PROGRAM
/	MONITORS ALL KEYBOARDS AND RESPONDS
/	TO ANY INPUT WITH A CANNED MESSAGE
/	WRITTEN BY R. B. BARTLEIN - 11/14/74
/
	*0
	0
	JMP I .+1
	INTER
	*10
XR1,	0
XR2,	0
	*20
CHGPTR,	0
CHKPTR,	0
K00PTR,	DEVLST+2	/MESSAGE POINTER FOR K00
NEWMSG,	EMSG
RUBFLG,	0
EDCHAR,	0
/
TYPE=	JMS .	/TYPE A CHARACTER
	0
	TLS
	CLA
	TSF
	JMP .-1
	JMP I .-5	/RETURN
/
AAAP0=	.-1
///
	*200
/
/START OF MAIN PROGRAM - WE MONITOR ALL TERMINALS AND
/PRINT OUR CANNED MESSAGE WHEN ANYONE TYPES A CHAR
START,	CLA CLL
	CAF		/CLEAR EVERYTHING INITIALLY
	TAD [CHGMSG-1
	DCA I K00PTR	/START MESSAGE ON CONSOLE
	TLS
	ION
	TAD [707
	DCA DISPLY	/SET UP THE AC DISPLAY
LOOP,	TAD LPCNT
	DCA TMP2	/SET SECOND LOOP COUNTER
	TAD DISPLY
	RAL
	ISZ TMP1
	JMP .-1
	ISZ TMP2
	JMP .-3
	DCA DISPLY
	TAD [DEVLST
	DCA TMP2	/SET THE LIST POINTER
KIELP,	TAD I TMP2	/GET AN INPUT DEVICE
	SNA		/END OF LIST?
	JMP LOOP	/YES - JUST WAIT
	TAD [6005	/NO - FORM THE 'KIE' INSTRUCTION
	DCA .+2
	CLA IAC
	 HLT		/NOW ENABLE INTERRUPTS
	CLA
	ISZ TMP2	/INCREMENT THE LIST POINTER
	ISZ TMP2
	ISZ TMP2
	JMP KIELP	/ AND ENABLE THE REST
/
DISPLY,	707
LPCNT,	-4
TMP1,	0
TMP2,	0
/
AAAP1=	.-1
///
/INTERRUPT HANDLING - WE JUST CONTINUE TYPING
/IF THIS IS AN OUTPUT INTERRUPT; IF THIS IS AN INPUT
/INTERRUPT, WE START TYPING THE "DOWN" MESSAGE.
INTER,	DCA AC
	RTR
	DCA LINK
	TAD [DEVLST
	DCA CHKPTR
INTER1,	TAD I CHKPTR	/GET THE INPUT DEVICE
	SNA		/END OF LIST?
	JMP INTER7	/YES - EXIT ROUTINE
	TAD [6001
	DCA INTER2	/SET THE INPUT CHECK
	ISZ CHKPTR
	TAD I CHKPTR	/GET THE OUTPUT DEVICE
	TAD [6006
	DCA INTER5	/SET THE OUTPUT SELECT
	ISZ CHKPTR
INTER2,	 HLT	/ KSF
	JMP INTER3	/NOT INPUT DEVICE
	CLA IAC
	TAD .-3
	DCA .+1
	 HLT	/ KCC
	TAD I CHKPTR	/GET THE MESSAGE INDEX
	SZA CLA		/ALREADY TYPING MESSAGE?
	JMP INTER6	/YES
	TAD K00PTR
	CIA
	TAD CHKPTR
	SNA CLA		/IS THIS THE CONSOLE (K00)?
	TAD [CHGMSG-MSG	/YES - ADD THE 'CHANGE' MESSAGE
	TAD [MSG-1	/NO - SET THE DOWN-MESSAGE
	DCA I CHKPTR	/SET THE MESSAGE POINTER
	JMP INTER4
INTER3,	TAD INTER5	/GET THE OUTPUT SELECT
	TAD [-5
	DCA .+1
	 HLT	/ TSF
	JMP INTER6	/NOPE - CONTINUE LOOP
	CLA IAC
	TAD .-3
	DCA .+1
	 HLT		/CLEAR THE FLAG
INTER4,	TAD I CHKPTR
	SNA		/ANY MESSAGE BEING TYPED?
	JMP INTER6	/NOPE - JUST EXIT
	IAC
	DCA I CHKPTR	/YES - INCREMENT THE ADDRESS
	TAD I CHKPTR
	DCA INTEMP	/SET THE MESSAGE ADDRESS
	TAD I INTEMP
	SZA		/END OF MESSAGE?
	JMP INTER5	/NO - TYPE THE CHARACTER
	DCA I CHKPTR	/YES - CLEAR THE POINTER
	JMP INTER6	/ AND EXIT
INTER5,	 HLT		/TYPE THE CHARACTER
	CLA CLL
INTER6,	ISZ CHKPTR
	SRQ		/ANY MORE INTERRUPTS?
	JMP INTER7	/NO - JUST EXIT
	JMP INTER1	/YES - CONTINUE CHECKING
/
/NOW WE RELOAD THE AC & LINK AND RETURN TO OUR MAINLINE.
INTER7,	CLA CLL
	TAD LINK
	CLL RTL
	CLA
	TAD AC
	ION
	JMP I 0		/EXIT INTERRUPT HANDLER
/
AC,	0
LINK,	0
INTEMP,	0
///
	*1000
/
/WE ARE STARTED HERE TO CHANGE THE "DOWN MESSAGE"
CHANGE,	CLA CLL
	DCA EDCHAR
	TAD ENDMSG
	DCA XR1
	TAD I XR1	/TYPE "END WITH ALT-MODE"
	SNA
	JMP .+3
	TYPE
	JMP .-4
	TAD NEWMSG	/INITIALIZE THE MESSAGE POINTER
	DCA CHGPTR
	TAD [MSG+2
	DCA XR1		/SET POINTER FOR CHARACTER SEARCH
	DCA RUBFLG
CHG2,	KSF		/NOW WAIT FOR A CHARACTER
	JMP .-1
	KRB
	AND [177
	TAD [200
	TAD [-377
	SNA		/RUBOUT?
	JMP RUBOUT	/YES
	DCA XR2		/NO - SAVE OUR CHAR A MOMENT
	ISZ RUBFLG	/JUST FINISH RUBOUTS?
	JMP .+3		/NO
	TAD ["\
	TYPE		/YES - CLOSE THE BLOCK
	DCA RUBFLG	/ AND CLEAR THE FLAG
	TAD XR2
	TAD [377-375
	SNA		/ALT MODE?
	JMP CHGEND	/YES
	TAD [375-376
	SNA		/ASR 35 ALT MODE?
	JMP CHGEND	/YES
	TAD [376-233
	SNA		/ESCAPE?
	JMP CHGEND	/YES
	TAD [233-232
	SNA		/CTRL/Z - EDIT COMMAND?
	JMP EDIT	/YES
	TAD [232-214
	SNA		/CTRL/L - CONTINUE SEARCH?
	JMP EDITC	/YES
	TAD [214
	DCA I CHGPTR	/NOW STORE THE CHARACTER
	TAD I CHGPTR
	TYPE		/NOW ECHO THE CHARACTER
	ISZ CHGPTR
	JMP CHG2	/GET THE NEXT CHARACTER
	JMP CHANGE	/RAN OUT OF ROOM - RESTART
/
/HERE WE HANDLE THE RUBOUT CONTROL
RUBOUT,	TAD NEWMSG
	CIA
	TAD CHGPTR
	SNA CLA		/ANYTHING TO RUB OUT?
	JMP CHG2	/NO - IGNORE THE RUBOUT
	ISZ RUBFLG	/PART OF A STRING?
	SKP		/NOT YET
	JMP .+3
	TAD ["\
	TYPE		/YES - TYPE "\"
	CLA CMA
	DCA RUBFLG	/ AND SET THE FLAG
	CLA CMA
	TAD CHGPTR
	DCA CHGPTR	/BACK UP THE POINTER
	TAD I CHGPTR
	TYPE		/ AND ECHO THE CHARACTER
	JMP CHG2	/NOW GET THE NEXT CHARACTER
/
/HERE WE TERMINATE THE NEW MESSAGE AND START THE
/MAIN PROGRAM.
CHGEND,	DCA I CHGPTR	/STORE A ZERO
	TAD [MSG+2
	DCA XR1		/SET THE DESTINATION POINTER
	CLA CMA
	TAD NEWMSG
	DCA XR2		/ AND THE NEW MSG POINTER
	TAD I XR2	/GET A CHARACTER
	SNA		/END OF MESSAGE?
	JMP CHGSET	/YES - SET FOR THE NEXT CHANGE
	DCA I XR1
	JMP .-4		/NO - MOVE THE NEXT CHARACTER
CHGSET,	DCA I XR1	/STORE A ZERO
	DCA I XR1
	TAD XR1		/GET THE END ADDRESS
	DCA NEWMSG	/ AND SAVE IT
	JMP I [200	/THEN START THE PROGRAM
/
/WE COME HERE TO SEARCH THE PREVIOUS MESSAGE
/AND COPY IT TO THE NEW MESSAGE.
EDIT,	TAD ["^
	TYPE		/TYPE A PROMPT
	KSF
	JMP .-1
	KRB		/READ THE SEARCH CHAR
	AND [177
	SNA		/ZERO?
	JMP .-5		/YES - THAT'S NOT ALLOWED
	TAD [200
	CIA
	DCA EDCHAR
/
/WE COME HERE TO CONTINUE A SINGLE CHARACTER SEARCH.
EDITC,	TAD EDCHAR
	SNA CLA		/IS THERE A SEARCH CHAR?
	JMP CHG2	/NO - IGNORE THE CTRL/L
EDIT1,	TAD I XR1	/GET THE NEXT MESSAGE CHARACTER
	SNA		/END OF MESSAGE?
	JMP EDIT2	/YES
	DCA I CHGPTR	/NO - STORE IT
	TAD I CHGPTR
	TYPE		/ AND ECHO IT
	TAD I CHGPTR
	ISZ CHGPTR	/INCREMENT THE MESSAGE POINTER
	SKP
	JMP CHANGE	/OUT OF ROOM - RESTART
	TAD EDCHAR
	SZA CLA		/IS IT OUR SEARCH CHAR?
	JMP EDIT1	/NO - KEEP GOING
	JMP CHG2	/YES - STOP HERE
/
/WE REACHED THE END OF THE OLD MESSAGE
EDIT2,	CLA CMA
	TAD XR1		/RESET THE POINTER
	DCA XR1		/ TO POINT TO THE END
	JMP CHG2	/ AND TERMINATE THE SEARCH
/
AAACHG=	.-1
/
ENDMSG,	.;215;212;212;"E;"N;"D;" ;"W;"I;"T;"H
	" ;"A;"L;"T;" ;"M;"O;"D;"E;215;212;0
///
DEVLST,	030; 040; 0	/K00
	400; 410; 0
	420; 430; 0	/K02
	440; 450; 0
	460; 470; 0	/K04
	340; 350; 0
	110; 120; 0	/K06
	300; 310; 0
	320; 330; 0	/K10
	500; 510; 0
	520; 530; 0	/K12
	540; 550; 0
	560; 570; 0	/K14
	700; 710; 0
	360; 370; 0	/K16
	720; 730; 0
	740; 750; 0	/K20
	060; 070; 0
	140; 150; 0
	160; 170; 0	/K23
	0		/END OF LIST
/
CHGMSG,	215;212;212;"S;"T;"O;"P;" ;"C;"P;"U;" ;"A;"N;"D
	" ;"S;"T;"A;"R;"T;" ;"A;"T;" ;"L;"O;"C;"A;"T;"I;"O;"N
	" ;"1;"0;"0;"0;" ;"T;"O;" ;"C;"H;"A;"N;"G;"E
	" ;"M;"E;"S;"S;"A;"G;"E;".
MSG,	215;212;212;"S;"Y;"S;"T;"E;"M;" ;"I;"S
	" ;"D;"O;"W;"N;";;" ;"P;"L;"E;"A;"S;"E
	" ;"H;"A;"N;"G;" ;"U;"P;" ;"N;"O;"W;215;212
	"A;"N;"D;" ;"C;"A;"L;"L;" ;"B;"A;"C;"K
	" ;"L;"A;"T;"E;"R;".;215;212
	0
EMSG=	.
/
AAAEND=	.-1
///	$$$
	$$$
